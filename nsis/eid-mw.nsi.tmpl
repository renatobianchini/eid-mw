!include "MUI2.nsh"
!include "x64.nsh"
Name "Belgian eID Middleware"
OutFile "eid-mw-_SVN_REVISION_.exe"

InstallDir "$PROGRAMFILES\Belgian eID"
!define REG_UNINSTALL "Software\Microsoft\Windows\CurrentVersion\Uninstall\beid-middleware"
!define BIN64 "src/dist/win64/usr/local/bin"
!define BIN32 "src/dist/win32/usr/local/bin"
!define SHARE32 "src/dist/win32/usr/local/share"

;Request application privileges for Windows Vista
RequestExecutionLevel admin

;--------------------------------
;Interface Settings
  !define MUI_ABORTWARNING
  ;Show all languages, despite user's codepage
  !define MUI_LANGDLL_ALLLANGUAGES

  ; pretty icons
!define MUI_ICON "src/meta/eid-mw-install.ico"
!define MUI_UNICON "src/meta/eid-mw-uninstall.ico"

;--------------------------------
;Language Selection Dialog Settings
  ;Remember the installer language
  !define MUI_LANGDLL_REGISTRY_ROOT "HKCU" 
  !define MUI_LANGDLL_REGISTRY_KEY "Software\beid-middleware" 
  !define MUI_LANGDLL_REGISTRY_VALUENAME "Installer Language"

;--------------------------------
;Pages
  !insertmacro MUI_PAGE_LICENSE "src/meta/COPYING"
  !insertmacro MUI_PAGE_COMPONENTS
  !insertmacro MUI_PAGE_INSTFILES
  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES

;--------------------------------
;Languages
  !insertmacro MUI_LANGUAGE "Dutch"   ;first language is the default language
  !insertmacro MUI_LANGUAGE "French"
  !insertmacro MUI_LANGUAGE "German"
  !insertmacro MUI_LANGUAGE "English" 


;--------------------------------
;Reserve Files
  
  ;If you are using solid compression, files that are required before
  ;the actual installation should be stored first in the data block,
  ;because this will make your installer start faster.
  
  !insertmacro MUI_RESERVEFILE_LANGDLL

;--------------------------------


;Installer Sections


; Core Libraries ; needed for pkcs#11 and CSP
; -----------------------------------------------------------------------------------------
Section "beID Core" SecCoreLibs

  SectionIn RO
  SetOutPath "$SYSDIR"

  ; 32-bit install:
  ${EnableX64FSRedirection}
  File ${BIN32}/libgcc_s_sjlj-1.dll
  File ${BIN32}/libstdc++-6.dll
  File ${BIN32}/libbeidcommon.dll
  File ${BIN32}/libbeiddialogs.dll
  File ${BIN32}/libbeidcardlayer.dll

	${If} ${RunningX64}
		; 64-bit install:
		${DisableX64FSRedirection}
  		File ${BIN64}/libgcc_s_sjlj-1.dll
  		File ${BIN64}/libstdc++-6.dll
  		File ${BIN64}/libbeidcommon.dll
  		File ${BIN64}/libbeiddialogs.dll
  		File ${BIN64}/libbeidcardlayer.dll
	${EndIf}

 
  WriteRegStr HKLM "${REG_UNINSTALL}" "DisplayName" "Belgian Electronic Identity Middleware"
  WriteRegStr HKLM "${REG_UNINSTALL}" "DisplayIcon" "$INSTDIR\Uninstall.exe"
  WriteRegStr HKLM "${REG_UNINSTALL}" "DisplayVersion" "1.0"
  WriteRegStr HKLM "${REG_UNINSTALL}" "Publisher" "FedICT"
  WriteRegStr HKLM "${REG_UNINSTALL}" "InstallSource" "$EXEDIR\"
  WriteRegDWord HKLM "${REG_UNINSTALL}" "NoModify" 1
  WriteRegDWord HKLM "${REG_UNINSTALL}" "NoRepair" 1
  WriteRegStr HKLM "${REG_UNINSTALL}" "UninstallString" '"$INSTDIR\Uninstall.exe"'

  CreateDirectory "$INSTDIR"
  WriteUninstaller "$INSTDIR\Uninstall.exe"
  SetOutPath "$INSTDIR"
SectionEnd

; pkcs11 module and Firefox extension ; for Firefox and other standards-aware apps
; -----------------------------------------------------------------------------------------
Section "pkcs#11 Module" SecPkcs11

	SetOutPath $SYSDIR
	; 32-bit install:
	${EnableX64FSRedirection}
  	File ${BIN32}/libbeidpkcs11.dll
	${If} ${RunningX64}
		; 64-bit install:
		${DisableX64FSRedirection}
  		File ${BIN64}/libbeidpkcs11.dll
	${EndIf}

	SetOutPath "$PROGRAMFILES\Mozilla Firefox\extensions"
	; 32-bit install:
	${EnableX64FSRedirection}
	File /r ${SHARE32}/belgiumeid@eid.belgium.be  # extracts to C:\Windows\SysWOW64 ; rename because registry reference
	${If} ${RunningX64}
		; 64-bit install:
		${DisableX64FSRedirection}
		File /r ${SHARE32}/belgiumeid@eid.belgium.be # extracts to C:\Windows\System32 ; rename because registry reference
	${EndIf}

SectionEnd

; minidriver ; for IE and other proprietary apps on Windows Vista and Above
; -----------------------------------------------------------------------------------------
Section "minidriver" SecMiniDriver
	SetOutPath "$SYSDIR"
	; 32-bit install:
	${EnableX64FSRedirection}
	File /oname=beidmdrv.dll ${BIN32}/libbeidmdrv32.dll  # extracts to C:\Windows\SysWOW64 ; rename because registry reference
	${If} ${RunningX64}
		; 64-bit install:
		${DisableX64FSRedirection}
		File /oname=beidmdrv.dll ${BIN64}/libbeidmdrv64.dll # extracts to C:\Windows\System32 ; rename because registry reference
	${EndIf}
SectionEnd


; CSP+Certificate Propagation Service ; for IE and other proprietary apps on Windows XP ; FIXME ADD THE READ CSP AND CERTPROP BINARIES HERE
; -----------------------------------------------------------------------------------------
Section "csp" SecCSPAndPropagationService
	SetOutPath $SYSDIR
	; 32-bit install:
	${EnableX64FSRedirection}
	File /oname=beidmdrv.dll ${BIN32}/libbeidmdrv32.dll  # extracts to C:\Windows\SysWOW64 ; rename because registry reference
	${If} ${RunningX64}
		; 64-bit install:
		${DisableX64FSRedirection}
		File /oname=beidmdrv.dll ${BIN64}/libbeidmdrv64.dll # extracts to C:\Windows\System32 ; rename because registry reference
	${EndIf}
SectionEnd


; SISCard ; for reading Belgian SIS cards on certain readers
; -----------------------------------------------------------------------------------------

Section "SIS Card Modules" SecSisCard
	SetOutPath $SYSDIR
	; 32-bit install:
	${EnableX64FSRedirection}
  	File ${BIN32}/libcardpluginbeid.dll
  	File ${BIN32}/libcardpluginsis_acr38u.dll
  	File ${BIN32}/libcardpluginsis.dll
	${If} ${RunningX64}
		; 64-bit install:
		${DisableX64FSRedirection}
  		File ${BIN64}/libcardpluginbeid.dll
  		File ${BIN64}/libcardpluginsis_acr38u.dll
  		File ${BIN64}/libcardpluginsis.dll
	${EndIf}
SectionEnd


;--------------------------------
;Installer Functions

Function .onInit
	!insertmacro MUI_LANGDLL_DISPLAY

	Push $R0
	Push $R1
	ClearErrors
	ReadRegStr $R0 HKLM "SOFTWARE\Microsoft\Windows NT\CurrentVersion" CurrentVersion
	IfErrors 0 lbl_winnt
 
; we are not NT
	ReadRegStr $R0 HKLM "SOFTWARE\Microsoft\Windows\CurrentVersion" VersionNumber
	StrCpy $R1 $R0 1
	StrCmp $R1 '4' 0 lbl_error
	StrCpy $R1 $R0 3
	StrCmp $R1 '4.0' lbl_win32_95
	StrCmp $R1 '4.9' lbl_win32_ME lbl_win32_98
 
lbl_win32_95:
	StrCpy $R0 '95'
	MessageBox MB_OK|MB_ICONQUESTION "Windows 95 is not supported by eid-mw" IDOK 
	Quit
    Goto lbl_done
 
lbl_win32_98:
	StrCpy $R0 '98'
	MessageBox MB_OK|MB_ICONQUESTION "Windows 98 is not supported by eid-mw" IDOK 
	Quit
    Goto lbl_done
 
lbl_win32_ME:
	StrCpy $R0 'ME'
	MessageBox MB_OK|MB_ICONQUESTION "Windows ME is not supported by eid-mw" IDOK 
	Quit
    Goto lbl_done
 
lbl_winnt:
	StrCpy $R1 $R0 1
	StrCmp $R1 '3' lbl_winnt_x
	StrCmp $R1 '4' lbl_winnt_x
	StrCpy $R1 $R0 3
	StrCmp $R1 '5.0' lbl_winnt_2000
	StrCmp $R1 '5.1' lbl_winnt_XP
	StrCmp $R1 '5.2' lbl_winnt_2003
	StrCmp $R1 '6.0' lbl_winnt_vista
	StrCmp $R1 '6.1' lbl_winnt_7 lbl_error
 
lbl_winnt_x:
	StrCpy $R0 "NT $R0" 6
	MessageBox MB_OK|MB_ICONQUESTION "Windows NT4 is not supported by eid-mw" IDOK 
	Quit
	Goto lbl_done
 
lbl_winnt_2000:
	Strcpy $R0 '2000'
	MessageBox MB_OK|MB_ICONQUESTION "Windows 2000 is not supported by eid-mw" IDOK 
	Quit
    Goto lbl_done
 
lbl_winnt_XP:
	Strcpy $R0 'XP'
	MessageBox MB_OK|MB_ICONQUESTION "Windows XP (-minidriver +CSP)" IDOK 
	SectionSetFlags ${SecMiniDriver} ${SF_RO}
	SectionSetFlags ${SecCSPAndPropagationService} ${SF_SELECTED}
    Goto lbl_done
 
lbl_winnt_2003:
	Strcpy $R0 '2003'
	MessageBox MB_OK|MB_ICONQUESTION "Windows 2003 (-minidriver +CSP)" IDOK 
	SectionSetFlags ${SecMiniDriver} ${SF_RO}
	SectionSetFlags ${SecCSPAndPropagationService} ${SF_SELECTED}
    Goto lbl_done
 
lbl_winnt_vista:
	Strcpy $R0 'Vista'
	MessageBox MB_OK|MB_ICONQUESTION "Windows Vista (+minidriver -CSP)" IDOK 
	SectionSetFlags ${SecMiniDriver} ${SF_SELECTED}
	SectionSetFlags ${SecCSPAndPropagationService} ${SF_RO}
    Goto lbl_done
 
lbl_winnt_7:
	Strcpy $R0 '7'
	MessageBox MB_OK|MB_ICONQUESTION "Windows 7 (+minidriver -CSP)" IDOK 
	SectionSetFlags ${SecMiniDriver} ${SF_SELECTED}
	SectionSetFlags ${SecCSPAndPropagationService} ${SF_RO}
	Goto lbl_done
 
	lbl_error:
	Strcpy $R0 ''
	lbl_done:
	Pop $R1
	Exch $R0
 
FunctionEnd


;--------------------------------
;Descriptions

LangString DESC_SecCoreLibs ${LANG_ENGLISH} "Core Libraries (required for other modules)"
LangString DESC_SecCoreLibs ${LANG_DUTCH}   "Basissoftware (vereist voor de andere modules)"
LangString DESC_SecCoreLibs ${LANG_FRENCH}  "Logiciel de base (prerequis pour les autre modules)"
LangString DESC_SecCoreLibs ${LANG_GERMAN}  "Basis Software (erfordert f√ºr andere Module)"

LangString DESC_SecPkcs11   ${LANG_ENGLISH} ".. for using eID from Standard Applications (e.g. Mozilla Firefox)"
LangString DESC_SecPkcs11   ${LANG_DUTCH}   ".. om eID met standaardtoepassingen (b.v. Mozilla Firefox) te gebruiken."
LangString DESC_SecPkcs11   ${LANG_FRENCH}  ".. pour utiliser eID avec des Applications Standard (comme Mozilla Firefox)"
LangString DESC_SecPkcs11   ${LANG_GERMAN}  ".. um eID mit Standard-Applikazionen zu benutzen (z.b. Mozilla Firefox)"

LangString DESC_SecMiniDriver    ${LANG_ENGLISH} ".. for using eID for other applications (Windows Vista and above)"
LangString DESC_SecMiniDriver    ${LANG_DUTCH}   ".. om eID in andere toepassingen te gebruiken (Windows Vista en hoger)"
LangString DESC_SecMiniDriver    ${LANG_FRENCH}  ".. pour utiliser eID dans d'autres applications (a partir de Windows Vista)"
LangString DESC_SecMiniDriver    ${LANG_GERMAN}  ".. um eID auf andere Applicazionen zu benutzen (Windows Vista und hoher)"

LangString DESC_SecCPSAndPropagationService ${LANG_ENGLISH} ".. for using eID for other applications (Windows XP)"
LangString DESC_SecCPSAndPropagationService ${LANG_DUTCH}   ".. om eID in andere toepassingen te gebruiken (Windows XP)"
LangString DESC_SecCPSAndPropagationService ${LANG_FRENCH}  ".. pour utiliser eID dans d'autres applications (Windows XP)"
LangString DESC_SecCPSAndPropagationService ${LANG_GERMAN}  ".. um eID auf andere Applicazionen zu benutzen (Windows XP)"

LangString DESC_SecSisCard   ${LANG_ENGLISH} ".. for reading SIS cards (on particular card readers"
LangString DESC_SecSisCard   ${LANG_DUTCH}   ".. om (met bepaalde kaartlezers) SIS-kaarten uit te lezen"
LangString DESC_SecSisCard   ${LANG_FRENCH}  ".. pour lire les cartes SIS (avec certains lecteurs de cartes"
LangString DESC_SecSisCard   ${LANG_GERMAN}  ".. um mit bestimmten Kartenlezer SIS-Karten zu lesen" 

!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SecCoreLibs} $(DESC_SecCoreLibs)
  !insertmacro MUI_DESCRIPTION_TEXT ${SecPkcs11}   $(DESC_SecPkcs11)
  !insertmacro MUI_DESCRIPTION_TEXT ${SecMiniDriver}   $(DESC_SecMiniDriver)
  !insertmacro MUI_DESCRIPTION_TEXT ${SecCSPAndPropagationService}   $(DESC_SecCPSAndPropagationService)
  !insertmacro MUI_DESCRIPTION_TEXT ${SecSisCard}  $(DESC_SecSisCard)
!insertmacro MUI_FUNCTION_DESCRIPTION_END

 
;--------------------------------
;Uninstaller Section

Section "Uninstall"

 ; 32-bit uninstall:
  ${EnableX64FSRedirection}
  Delete "$SYSDIR\libbeidcommon.dll"
  Delete "$SYSDIR\libbeiddialogs.dll"
  Delete "$SYSDIR\libbeidcardlayer.dll"
  Delete "$SYSDIR\beidmdrv.dll"
  Delete "$SYSDIR\libbeidpkcs11.dll"
  Delete "$SYSDIR\libcardpluginbeid.dll"
  Delete "$SYSDIR\libcardpluginsis_acr38u.dll"
  Delete "$SYSDIR\libcardpluginsis.dll"
  RmDir /r "${SHARE32}/belgiumeid@eid.belgium.be"

        ${If} ${RunningX64}
                ; 64-bit install:
                ${DisableX64FSRedirection}
		Delete "$SYSDIR\libbeidcommon.dll"
		Delete "$SYSDIR\libbeiddialogs.dll"
		Delete "$SYSDIR\libbeidcardlayer.dll"
		Delete "$SYSDIR\beidmdrv.dll"
		Delete "$SYSDIR\libbeidpkcs11.dll"
		Delete "$SYSDIR\libcardpluginbeid.dll"
		Delete "$SYSDIR\libcardpluginsis_acr38u.dll"
		Delete "$SYSDIR\libcardpluginsis.dll"
		RMDir /r "${SHARE32}/belgiumeid@eid.belgium.be"
        ${EndIf}


  RMDIR /r $INSTDIR
  DeleteRegKey HKLM "${REG_UNINSTALL}"
SectionEnd

;--------------------------------
;Uninstaller Functions

Function un.onInit
  !insertmacro MUI_UNGETLANGUAGE
FunctionEnd
